# Copyright 2020 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/fuchsia-sdk/sdk/build/package.gni")
import("//third_party/fuchsia-sdk/sdk/build/component.gni")

# Define a Fuchsia Node Component
# Parameters
#
#   manifest:
#     Required: The source manifest file for this component. This can be
#     either a v1 (.cmx) or v2 (.cml) manifest. v2 manifests must have
#     the file extension ".cml".
#
#   sources:
#     Optional: JS files to be included in the package.
#
template("node_component") {
  assert(defined(invoker.manifest), "manifest file required for this component")

  copy("$target_name.sources") {
    sources = invoker.sources
    outputs = [ "$root_gen_dir/{{source_target_relative}}" ]
  }

  fuchsia_component("$target_name") {
    manifest = invoker.manifest
    data_deps = [ "//node:node" ]
    if (defined(invoker.sources)) {
      data_deps += [ ":$target_name.sources" ]
    }
  }
}