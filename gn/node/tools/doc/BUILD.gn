# Copyright 2019 the V8 project authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//node/node_files.gni")
import("//node/node.gni")

sync_files("sync_files") {
  sources = node_files.tools_doc_files
}

copy("copy_addons_md") {
  sources = [ "//node/doc/api/addons.md" ]
  outputs = [ "$root_gen_dir/node/doc/api/addons.md" ]
}

call_node("npm_install_dependencies") {
  deps = [ ":sync_files" ]
  inputs = [
    npm_cli,
    "$target_gen_dir/package.json"
  ]
  base = target_gen_dir
  args = [
    rebase_path(npm_cli, base),
    "ci",
  ]
}

action("generate_addons") {
  deps = [
    ":npm_install_dependencies",
    ":sync_files",
    ":copy_addons_md",
    "//node/test/addons:sync_files",
    "//node:node",
  ]
  script = "//tools/run_executable_in_dir.py"
  inputs = [
    "$target_gen_dir/addon-verify.js",
    "//node/doc/api/addons.md",
  ]
  stamp = "$target_gen_dir/generate_addons.stamp"
  outputs = [ stamp ]
  base = root_gen_dir
  args = [
    rebase_path(base, root_build_dir),
    rebase_path(stamp, root_build_dir),
    rebase_path(node_exe, base),
  ] + rebase_path(inputs, base)
}
